#!/usr/bin/env foma -f

read lexc toy.lexc
def Lexicon

define InsertMorphBoundary [..] -> "^" || Alphabet _ MorphPhonSymbols ;

define Stop                [  p   |   t                 |  k     | "kw"   |  q    | "qw"       ] ;
define VoicedFricative     [  v   |   l  | z | y |  r   |  g     |  w     | "gh"  | "ghw"      ] ;
define VoicelessFricative  [  f   | "ll" |   s   | "rr" | "gg"   | "wh"   | "ghh" | "ghhw" | h ] ;
define VoicedNasal         [  m   |   n                 | "ng"   | "ngw"                       ] ;
define VoicelessNasal      [ "mm" | "nn"                | "ngng" | "ngngw"                     ] ;

define Fricative [ VoicelessFricative | VoicedFricative ] ;
define Nasal     [ VoicelessNasal     | VoicedNasal     ] ;

define WordBoundary [ .#. ] ;

define C         [ Stop | Fricative | Nasal ] ;        

define FullVowel   [ a   | i   | u   ] ;
define DoubleVowel [ a a | i i | u u ] ;
define V           [ e | FullVowel ];

define Alphabet [ C | V ];
define MorphPhonSymbols [ "~" | ":" | "(ng)" | "~f" | "(g/t)" ];



define FinalE [..] -> a || WordBoundary (C) a _ C e   "~f"   ∘  ! e-hopping
              [..] -> i || WordBoundary (C) i _ C e   "~f"   ∘  ! e-hopping
              [..] -> u || WordBoundary (C) u _ C e   "~f"   ∘  ! e-hopping
                 e -> 0 ||                    V C _   "~f"   ∘  ! e-dropping
                 e -> 0 ||                        _   "~f" V ∘  ! e-dropping
			  "~f" -> 0 || WordBoundary Alphabet* _         ;

define SemiAndFinalE [..] -> a || WordBoundary (C) a _ C e   "~"   ∘  ! final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e   "~"   ∘  ! final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e   "~"   ∘  ! final e-hopping
                        e -> 0 ||                    V C _   "~"   ∘  ! final e-dropping
                        e -> 0 ||                        _   "~" V ∘  ! final e-dropping  
                     [..] -> a || WordBoundary (C) a _ C e C "~"   ∘  ! semi-final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e C "~"   ∘  ! semi-final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e C "~"   ∘  ! semi-final e-hopping
                        e -> 0 ||                    V C _ C "~"   ∘  ! semi-final e-dropping
					  "~" -> 0 || WordBoundary Alphabet* _        ;

define JunctureConsonantInsertion "(ng)" -> "ng"         || V ("~") ":" _           ∘  ! (ng) used with bases that end in a vowel
                                  ["(g/t)" | "(g)"] -> g || V V [("~f") | ("~")] _  ∘  ! (g) used with bases that end in a double vowel
                                  ["(g/t)" | "(t)"] -> t ||             C ("~f") _  ∘  ! (t) used with bases that end in a consonant
								            "(ng)"  -> 0 || WordBoundary Alphabet* MorphPhonSymbols* _    ∘  
                                           "(g/t)"  -> 0 || WordBoundary Alphabet* MorphPhonSymbols* _    ;  


define UvularDropping gh ":" e -> a || [ WordBoundary | C ] a _    ∘
                      gh ":" e -> i || [ WordBoundary | C ] i _    ∘
                      gh ":" e -> u || [ WordBoundary | C ] u _    ∘
                      gh ":" a -> a || [ WordBoundary | C ] FullVowel _    ∘
                      gh ":" i -> i || [ WordBoundary | C ] FullVowel _    ∘
                      gh ":" u -> u || [ WordBoundary | C ] FullVowel _    ∘
					       ":" -> 0 || WordBoundary Alphabet* _             ;

define VowelDominance a i -> i i  ∘
                      i a -> i i  ∘
                      u i -> i i  ∘
                      i u -> i i  ∘
                      a u -> a a  ∘
                      u a -> a a  ;


define Rules [
	Lexicon ∘
	InsertMorphBoundary ∘
    JunctureConsonantInsertion ∘
	SemiAndFinalE ∘
	UvularDropping ∘
	FinalE      ∘
    VowelDominance ∘
	JunctureConsonantInsertion ∘
	SemiAndFinalE ∘
	UvularDropping ∘
	FinalE      ∘
    VowelDominance
];

define Grammar [ Rules ];
push Grammar
pairs
