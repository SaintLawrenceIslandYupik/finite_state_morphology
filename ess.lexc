Multichar_Symbols ngngw ngng ghhw ghh ghw ngw gg gh kw ll mm ng nn rr qw wh
		  [N] [V] [Sg] [Pl] [Du] [Abs] [Abl_Mod] [Unpd] [Intr_Ind] [1Sg] [1Du] [1Pl] [2Sg] [2Du] [2Pl] [3Sg] [3Du] [3Pl] [N→V]
                  ~sf ~f -w (e) (g/t) (ng)

LEXICON Root

        NounBase ;
        VerbBase ;




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                                            !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                Noun bases                  !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                                            !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounBase

! Jacobson (2001) Chapter 2 Vocabulary
afsengagh*       Noun; ! vole, locally mouse
aghnagh          Noun; ! woman
aghnegh          Noun; ! day
amaa             Noun; ! wolf
angyagh          Noun; ! boat
anipa            Noun; ! owl
ategh            Noun; ! name
atkug            Noun; ! parka
ayvegh           Noun; ! walrus
ggute            Noun; ! tooth
iye              Noun; ! eye
kameg            Noun; ! skin boot, mukluk
kaviigh          Noun; ! fox
kemegh           Noun; ! flesh, skin
kii              Noun; ! wound
kiigh            Noun; ! summer
kiiw             Noun; ! river
mangteghagh      Noun; ! house
ne               Noun; ! home
nanugh           Noun; ! polar bear
naayghagh        Noun; ! mountain
neghqwaagh       Noun; ! bone
qikmigh          Noun; ! dog
qiighw           Noun; ! grey hair , cough
qulmesiite       Noun; ! cooking pot
repa             Noun; ! hammer
riigte           Noun; ! animal's den
savig            Noun; ! knife
sigute           Noun; ! ear
siku             Noun; ! ice
siqugh*          Noun; ! needle
tafsi            Noun; ! belt
taghnughhagh*    Noun; ! child
ulaagh           Noun; ! semi-lunar knife, woman's knife, ulu
uupa             Noun; ! sea peach
uyghag           Noun; ! stone, rock
yaquleg          Noun; ! angel
yughaghte        Noun; ! minister, clergyman
yug              Noun; ! person, man (male human)
Yupig            Noun; ! Siberian / St. Lawrence Island Eskimo person


! Jacobson (2001) Chapter 3 Vocabulary
aghvegh          Noun; ! whale
anigu            Noun; ! snow on ground
aqavzig          Noun; ! salmonberry, cloudberry (Rubus chamaemorus)
aqelqagh         Noun; ! visitor
esla             Noun; ! outdoors; weather; awareness; world
iqallug          Noun; ! fish
kayu             Noun; ! sculpin; ocean fish (in general)
kufi             Noun; ! coffee
Laluramke        Noun; ! white person, Caucasian
megh             Noun; ! water
naayvagh         Noun; ! lake
neghsagh         Noun; ! seal (in general)
neqe             Noun; ! food
nuna             Noun; ! land
pagunghagh*      Noun; ! crowberry (Empetrum nigrum); berry (in general)
sikig            Noun; ! squirrel
Sivungagh*       Noun; ! Savoonga
Sivuqagh         Noun; ! Gambell; St. Lawrence Island (in general)
ukazig           Noun; ! hare, locally rabbit
ulghaagh         Noun; ! sea lion
Ungazigh         Noun; ! Old Chaplino, Indian Point (in Chukotka)
uqugh            Noun; ! fat; blubber
veg              Noun; ! grass
vegaagh          Noun; ! grass
yavlu            Noun; ! sweetheart
yaywaali         Noun; ! orphan

! Jacobson (2001) Chapter 4 Vocabulary
aghnaghagh*      Noun; ! girl
aaw              Noun; ! blood
ape              Noun; ! grandfather
ata              Noun; ! father
avaqutagh        Noun; ! child, offspring
eltughagh*       Noun; ! grandchild
iigge            Noun; ! hand
ighnegh          Noun; ! son
mekestaaghhagh*  Noun; ! small thing
mekelghiigh      Noun; ! boy
naa              Noun; ! mother
nengyugh*        Noun; ! grandmother
nuliigh          Noun; ! wife
nuyagh           Noun; ! hair
panig            Noun; ! daughter
seghlegh         Noun; ! bad thing
takestaaghhagh*  Noun; ! short thing
ugi              Noun; ! husband



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                                                                              !
!       Derivational morphemes applied to noun bases and derived bases         !
!                                                                              !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON Noun

! Noun → Noun postbases
[N→N]:–ghllag     Noun; ! big N; lots of N (when used in the plural)
[N→N]:–ghrugllag  Noun; ! big N; huge N
[N→N]:–leg        Noun; ! one having N

! Noun → Verb postbases
[N→V]:~%:(ng)u    Verb; ! to be N

0:0               NounInfl;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!                                                                              !
!       Inflectional morphemes applied to noun bases and derived bases         !
!                                                                              !
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
LEXICON NounInfl

[N][Abs][Sg]:0           #;
[N][Abs][Pl]:~sf-w%:(e)t #;
[N][Abs][Du]:~sf-w%:(e)k #;

[N][Abl_Mod][Unpd][Sg]:~f-wmeng #;

!.ABS_1SG-POSS_SG-POSD:~-ka #;






!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                                            !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                Verb bases                  !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!                                            !!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON VerbBase

aghula           Verb; ! to dance
aghulaqe         Verb; ! to leave
alinge           Verb; ! to be afraid
aane             Verb; ! to go out
aange            Verb; ! to be big
aqii             Verb; ! to take a pleasure trip, to take a vacation
esghaate         Verb; ! to see something
esleqe           Verb; ! to be full
gaagh            Verb; ! to cook
ifla             Verb; ! to lose; to get lost
ingaghte         Verb; ! to lie down
inghu            Verb; ! to pick berries
itegh            Verb; ! to go in, to enter
kaate            Verb; ! to arrive
kuuve            Verb; ! to spill
megh             Verb; ! to drink
nagate           Verb; ! to listen; to hear
negh             Verb; ! to eat
nenglagh         Verb; ! to laugh
pinigh           Verb; ! to be good
qavagh           Verb; ! to sleep
qiighw           Verb; ! to cough
qiya             Verb; ! to cry
tagi             Verb; ! to come towards speaker
taake            Verb; ! to be long
tuqu             Verb; ! to die

! Jacobson (2001) Chapter 4 Vocabulary
agatagh-         Verb; ! to be hungry
ayatagh-         Verb; ! to be hungry

LEXICON Verb

[V][Intr_Ind]:~f(g/t)u   VerbInfl; 


LEXICON VerbInfl

[3Sg]:q    #;
[3Pl]:t    #; 
[3Du]:k    #;
[1Sg]:nga  #;
[1Pl]:kut  #;
[1Du]:kung #;
[2Sg]:ten  #;
[2Pl]:si   #;
[2Du]:tek  #;


