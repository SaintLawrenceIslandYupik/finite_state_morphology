Multichar_Symbols ngngw ngng ghhw ghh ghw ngw gg gh kw ll mm ng nn rr qw wh
		  [N] [V] [Sg] [Pl] [Du] [Abs] [Abl_Mod] [Unpd] [Intr_Ind] [1Sg] [1Du] [1Pl] [2Sg] [2Du] [2Pl] [3Sg] [3Du] [3Pl] [N→N] [N→V]
		  [Abs1SgPoss] [SgPosd]
                  ~sf ~f -w (e) (g/t) (ng) (p/v) (t/y) (s/z)

LEXICON Root

        NounBase ;
        VerbBase ;




!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!                                            !!!!!!
!!!!!!                Noun Bases                  !!!!!!
!!!!!!                                            !!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounBase

! Jacobson (2001) Chapter 2 Vocabulary
afsengagh*       Noun; ! vole, locally mouse
aghnagh          Noun; ! woman
aghnegh          Noun; ! day
amaa             Noun; ! wolf
angyagh          Noun; ! boat
anipa            Noun; ! owl
ategh            Noun; ! name
atkug            Noun; ! parka
ayvegh           Noun; ! walrus
ggute            Noun; ! tooth
iye              Noun; ! eye
kameg            Noun; ! skin boot, mukluk
kaviigh          Noun; ! fox
kemeg            Noun; ! flesh, skin
kii              Noun; ! wound
kiig             Noun; ! summer
kiiw             Noun; ! river
mangteghagh      Noun; ! house
manig            Noun; ! egg
ne               Noun; ! home
nanugh           Noun; ! polar bear
naayghagh        Noun; ! mountain
neghqwaagh       Noun; ! bone
piitute          Noun; ! seed
qayagh           Noun; ! kayak
qikmigh          Noun; ! dog
qiighw           Noun; ! grey hair , cough
qulmesiite       Noun; ! cooking pot
repa             Noun; ! hammer
riigte           Noun; ! animal's den
saaghw           Noun; ! side
savig            Noun; ! knife
sigute           Noun; ! ear
siku             Noun; ! ice
sikugh*          Noun; ! needle
tafsi            Noun; ! belt
taghnughhagh*    Noun; ! child
taqegh           Noun; ! vein
ughluvegh        Noun; ! bow
ulaagh           Noun; ! semi-lunar knife, woman's knife, ulu
uupa             Noun; ! sea peach
uyghag           Noun; ! stone, rock
yaquleg          Noun; ! angel
yughaghte        Noun; ! minister, clergyman
yug              Noun; ! person, man (male human)
Yupig            Noun; ! Siberian / St. Lawrence Island Eskimo person

! Jacobson (2001) Chapter 3 Vocabulary
aghvegh          Noun; ! whale
anigu            Noun; ! snow on ground
aqavzig          Noun; ! salmonberry, cloudberry (Rubus chamaemorus)
aqelqagh         Noun; ! visitor
esla             Noun; ! outdoors; weather; awareness; world
iqallug          Noun; ! fish
kayu             Noun; ! sculpin; ocean fish (in general)
kufi             Noun; ! coffee
Laluramke        Noun; ! white person, Caucasian
megh             Noun; ! water
naayvagh         Noun; ! lake
neghsagh         Noun; ! seal (in general)
neqe             Noun; ! food
nuna             Noun; ! land
pagunghagh*      Noun; ! crowberry (Empetrum nigrum); berry (in general)
sikig            Noun; ! squirrel
Sivungagh*       Noun; ! Savoonga
Sivuqagh         Noun; ! Gambell; St. Lawrence Island (in general)
ukazigh          Noun; ! hare, locally rabbit
ulghaagh         Noun; ! sea lion
Ungazigh         Noun; ! Old Chaplino, Indian Point (in Chukotka)
uqugh            Noun; ! fat; blubber
veg              Noun; ! grass
vegaagh          Noun; ! grass
yavlu            Noun; ! sweetheart
yaywaali         Noun; ! orphan

! Jacobson (2001) Chapter 4 Vocabulary
aghnaghagh*      Noun; ! girl
aaw              Noun; ! blood
angyagh          Noun; ! boat
apa              Noun; ! grandfather
ata              Noun; ! father
ategh            Noun; ! name
avaqutagh        Noun; ! child, offspring
eltughagh*       Noun; ! grandchild
ggute            Noun; ! tooth
iigge            Noun; ! hand
ighnegh          Noun; ! son
kemeg            Noun; ! skin
mekestaaghhagh*  Noun; ! small thing
mekelghiigh      Noun; ! boy
naa              Noun; ! mother
ne               Noun; ! home
nengyugh*        Noun; ! grandmother
nuliigh          Noun; ! wife
nuyagh           Noun; ! hair
panig            Noun; ! daughter
qiighw           Noun; ! grey hair
seghlegh         Noun; ! bad thing
sigute           Noun; ! ear
takestaaghhagh*  Noun; ! short thing
ugi              Noun; ! husband

! Jacobson (2001) Chapter 5 Nouns 
!quunpeng          ? ; ! always (particle) 

aakagh*          Noun; ! older sister of a female
anengagh         Noun; ! older brother
elngaatall       Noun; ! very much (particle)
estugagh*        Noun; ! store
igaghvig         Noun; ! school 
iyaghvig         Noun; ! school
itegagh          Noun; ! foot 
naasqugh         Noun; ! head 
nayag            Noun; ! sister of a male 
ngelqagh         Noun; ! cormorant 
nunivag          Noun; ! rosewort plant (Sedum rosea), greens, tundra 
papeg            Noun; ! tail
puu              Noun; ! handle
qanegh           Noun; ! mouth (anatomical) 
qengagh          Noun; ! nose 
suflugagh        Noun; ! gun 
tepe             Noun; ! odor 
tukugh*          Noun; ! host 
tume             Noun; ! track, footprint; trail 
unaami           Noun; ! tomorrow (particle) 
uyughagh*        Noun; ! younger sibling (sister or borther)
yughaghvig       Noun; ! church

! Jacobson (2001) Chapter 6 Nouns 
aghnalqwaagh     Noun; ! old lady
akuutagh         Noun; ! mixture of berries, seal oil, sugar, etc., "Eskimo ice cream"
alangqugh        Noun; ! ring finger
apeghtughista    Noun; ! teacher
asag             Noun; ! paternal aunt
ighaleqetaagh    Noun; ! star
ila              Noun; ! relative; associate; part; one or some (of)
Karake           Noun; ! Koryak
lalala           Noun; ! dew
mesuuna          Noun; ! hairbrush; comb
nulugh           Noun; ! tea bag
paye             Noun; ! mouth (non-anatomical); hatch of kayak
pengugh          Noun; ! mound
qagugh           Noun; ! forehead
qaye             Noun; ! surface; top
qayugh           Noun; ! tea; broth
siivanllegh      Noun; ! elder
siqinegh         Noun; ! sun
tanqigh          Noun; ! moon; month
teghikusaq       Noun; ! animal
tengegkayug      Noun; ! airplane (plural, 'tengegkayuget', often used for one airplane)
ungipaghagh      Noun; ! traditional story; tale; legend
uyaqugh          Noun; ! neck
walguyaag        Noun; ! snowshoe



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!                                                                  !!!!!!
!!!!!!      Derivational Morphemes Applied To Noun / Derived Bases      !!!!!!
!!!!!!                                                                  !!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Noun

! Noun → Noun postbases
[N→N]:–ghllag     Noun; ! big N; lots of N (when used in the plural)
[N→N]:–ghrugllag  Noun; ! big N; huge N
[N→N]:–leg        Noun; ! one having N

! Noun → Verb postbases
[N→V]:~%:(ng)u    Verb; ! to be N

0:0               NounInfl;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!                                                                  !!!!!!
!!!!!!      Inflectional Morphemes Applied To Noun / Derived Bases      !!!!!!
!!!!!!                                                                  !!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON NounInfl

! -----------------------------------------------------------------------------------------------------------------------------------------------|
!                                                    Absolutive case endings                                                                     |
! -----------------------------------------------------------------------------------------------------------------------------------------------|
!        singular possessed                |           plural possessed                     |     dual possessed                                 |    
! -----------------------------------------------------------------------------------------------------------------------------------------------|-------------------------------|
[N][Abs][Sg]:0                            #;      [N][Abs][Pl]:~sf-w%:(e)t                  #;      [N][Abs][Du]:~sf-w%:(e)k                  #; ! unpossessed                   |
! -----------------------------------------|-------------------------------------------------|---------------------------------------------------|-------------------------------|
[N][Abs][3SgPoss][SgPosd]:~sf%:(ng)a      #;      [N][Abs][3SgPoss][PlPosd]:~sf%:(ng)i      #;      [N][Abs][3SgPoss][DuPosd]:~f-wkek         #; ! 3rd person singular possessor |
[N][Abs][3PlPoss][SgPosd]:~%:(ng)at       #;      [N][Abs][3PlPoss][PlPosd]:~%:(ng)it       #;      [N][Abs][3PlPoss][DuPosd]:~sf-w%:(e)gket  #; ! 3st person plural possessor   |
[N][Abs][3DuPoss][SgPosd]:~:(ng)ak        #;      [N][Abs][3DuPoss][PlPosd]:~:(ng)ik        #;      [N][Abs][3DuPoss][DuPosd]:~sf-w%:(e)gkek  #; ! 3rd person dual possessor     |
! -----------------------------------------|-------------------------------------------------|---------------------------------------------------|-------------------------------|
[N][Abs][1SgPoss][SgPosd]:~–ka            #;      [N][Abs][1SgPoss][PlPosd]:~sf-w%:(e)nka   #;      [N][Abs][1SgPoss][DuPosd]:~sf-w%:(e)gka   #; ! 1st person singular possessor |
[N][Abs][1PlPoss][SgPosd]:(p/v)ut         #;      [N][Abs][1PlPoss][PlPosd]:–put            #;      [N][Abs][1PlPoss][DuPosd]:~sf-w%:(e)gput  #; ! 1st person plural possessor   |
[N][Abs][1DuPoss][SgPosd]:(p/v)ung        #;      [N][Abs][1DuPoss][PlPosd]:–pung           #;      [N][Abs][1DuPoss][DuPosd]:~sf-w%:(e)gpung #; ! 1st person dual possessor     |
! -----------------------------------------|-------------------------------------------------|---------------------------------------------------|-------------------------------|
[N][Abs][2SgPoss][SgPosd]:~sf-w%:(e)n     #;      [N][Abs][2SgPoss][PlPosd]:ten             #;      [N][Abs][2SgPoss][DuPosd]:~sf-w%:(e)gken  #; ! 2nd person singular possessor |
[N][Abs][2PlPoss][SgPosd]:(s/z)i          #;      [N][Abs][2PlPoss][PlPosd]:–si             #;      [N][Abs][2PlPoss][DuPosd]:~sf-w%:(e)gsi   #; ! 2st person plural possessor   |
[N][Abs][2DuPoss][SgPosd]:(t/y)ek         #;      [N][Abs][2DuPoss][PlPosd]:–tek            #;      [N][Abs][2DuPoss][DuPosd]:~sf-w%:(g)tek   #; ! 2st person dual possessor     |
! -----------------------------------------|-------------------------------------------------|---------------------------------------------------|-------------------------------|
[N][Abs][4SgPoss][SgPosd]:–ni             #;      [N][Abs][4SgPoss][PlPosd]:–ni             #;      [N][Abs][4SgPoss][DuPosd]:~sf-w%:(e)gni   #; ! 4th person singular possessor |
[N][Abs][4PlPoss][SgPosd]:(t/y)eng        #;      [N][Abs][4PlPoss][PlPosd]:–teng           #;      [N][Abs][4PlPoss][DuPosd]:~sf-w%:(e)gtek  #; ! 4st person plural possessor   |
[N][Abs][4DuPoss][SgPosd]:(t/y)ek         #;      [N][Abs][4DuPoss][PlPosd]:–tek            #;      [N][Abs][4DuPoss][DuPosd]:~sf-w%:(e)gtek  #; ! 4st person dual possessor     |
! -----------------------------------------|-----------------------------------------------------------------------------------------------------|-------------------------------|


[N][Abl_Mod][Unpd][Sg]:~f-wmeng #;

! TODO: Final 'e' does not drop from short bases to form plural localis/terminalis cases
[N][Loc][Unpd][Sg]:~f-wmi           #;  ! LOCALIS CASE     at N
[N][Loc][Unpd][Pl]:~f-wni           #;
 
[N][Ter][Unpd][Sg]:~f-wmun          #;  ! TERMINALIS CASE  to N
[N][Ter][Unpd][Pl]:~f-wnun          #;
 
[N][Via][Unpd][Sg]:~f-wkun          #;  ! VIALIS CASE      through N
[N][Via][Unpd][Pl]:~sf-w%:(e)tgun   #;
 
[N][Equ][Unpd][Sg]:~f-wtun          #;  ! EQUALIS CASE     like N
[N][Equ][Unpd][Pl]:~sf-w%:(e)stun   #;

[N][Rel][Unpd][Sg]:~sf-w%:(e)m      #;  ! RELATIVE CASE    indicates posssesor of noun with 3rd person possessed ending
[N][Rel][Unpd][Pl]:~sf-w%:(e)t      #;
[N][Rel][Unpd][Du]:~sf-w%:(e)k      #;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!                                            !!!!!!
!!!!!!                Verb Bases                  !!!!!!
!!!!!!                                            !!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON VerbBase

aghula           Verb; ! to dance
aghulaqe         Verb; ! to leave
alinge           Verb; ! to be afraid
aane             Verb; ! to go out
aange            Verb; ! to be big
aqii             Verb; ! to take a pleasure trip, to take a vacation
esghaate         Verb; ! to see something
esleqe           Verb; ! to be full
gaagh            Verb; ! to cook
ifla             Verb; ! to lose; to get lost
ingaghte         Verb; ! to lie down
inghu            Verb; ! to pick berries
itegh            Verb; ! to go in, to enter
kaate            Verb; ! to arrive
kuuve            Verb; ! to spill
megh             Verb; ! to drink
nagate           Verb; ! to listen; to hear
negh             Verb; ! to eat
nenglagh         Verb; ! to laugh
pinigh           Verb; ! to be good
qavagh           Verb; ! to sleep
qiighw           Verb; ! to cough
qiya             Verb; ! to cry
tagi             Verb; ! to come towards speaker
taake            Verb; ! to be long
tuqu             Verb; ! to die

! Jacobson (2001) Chapter 4 Vocabulary
agatagh         Verb; ! to be hungry
ayatagh         Verb; ! to be hungry

LEXICON Verb

[V][Intr_Ind]:~f(g/t)u   VerbInfl; 


LEXICON VerbInfl

[3Sg]:q    #;
[3Pl]:t    #; 
[3Du]:k    #;
[1Sg]:nga  #;
[1Pl]:kut  #;
[1Du]:kung #;
[2Sg]:ten  #;
[2Pl]:si   #;
[2Du]:tek  #;
