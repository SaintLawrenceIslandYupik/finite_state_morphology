#!/usr/bin/env foma -f

read lexc exceptions.lexc
def Exceptions

read lexc ess.lexc

eliminate flag VALENCE
define LexiconNoFlags ;
define Lexicon LexiconNoFlags ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                                       !!!
!!!      Phoneme/Grapheme Definition      !!!
!!!                                       !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define WordBoundary [ .#. ] ;
define MorphemeBoundary "^" ;


define Stop               [  p   |   t                 |  k     | "kw"   |  q    | "qw"       ] ;
define VoicedFricative    [  v   |   l  | z | y |  r   |  g     |  w     | "gh"  | "ghw"      ] ;
define VoicelessFricative [  f   | "ll" |   s   | "rr" | "gg"   | "wh"   | "ghh" | "ghhw" | h ] ;
define VoicedNasal        [  m   |   n                 | "ng"   | "ngw"                       ] ;
define VoicelessNasal     [ "mm" | "nn"                | "ngng" | "ngngw"                     ] ;

define Fricative [ VoicelessFricative | VoicedFricative ] ;
define Nasal     [ VoicelessNasal     | VoicedNasal     ] ;
define DoubleFric [ "ll" | "rr" | "gg" | "ghh" | "ghhw" ] ;

define C         [ Stop | Fricative | Nasal ] ;        

define FullVowel   [ a   | i   | u   ] ;
define DoubleVowel [ a a | i i | u u ] ;
define V           [ e | FullVowel ];

define Alphabet [ C | V | "*" ] ;
define MorphPhonSymbols [ "~"     | "~f"   | "~sf"   | "~h"    | "-w"   | ":" | "–" | "@" | "@*" |
                          "(e)"   | "(g)"  | "(t)"   | "(g*)"  |
						  "(g/t)" | "(ng)" | "(p/v)" | "(s/z)" | "(t/y)" ] ;

define Lowercase "A" -> a ,
	             "I" -> i ,
				 "U" -> u ,
				 "E" -> e ,
				 "P" -> p ,
				 "T" -> t ,
				 "K" -> k ,
				 "Kw" -> kw ,
				 "Q" -> q ,
				 "Qw" -> qw,
				 "V" -> v ,
				 "L" -> l ,
				 "Z" -> z ,
                 "Y" -> y , 
                 "R" -> r ,
				 "G" -> g ,
				 "W" -> w ,
				 "Gh" -> gh ,
				 "Ghw" -> ghw ,
				 "F" -> f ,
				 "Ll" -> ll ,
				 "S" -> s ,
				 "Rr" -> rr ,
				 "Gg" -> gg ,
				 "Wh" -> wh ,
				 "Ghh" -> ghh ,
				 "Ghhw" -> ghhw ,
                 "M" -> m ,
				 "N" -> n ,
				 "Ng" -> ng ,
				 "Ngw" -> ngw ,
				 "Mm" -> mm ,
				 "Nn" -> nn ,
				 "Ngng" -> ngng ,
				 "Ngngw" -> ngngw ;

define InsertMorphBoundary [..] -> MorphemeBoundary || Alphabet _ MorphPhonSymbols ;
define CleanupMorphBoundary MorphemeBoundary -> 0 || WordBoundary Alphabet* _ ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                                    !!!
!!!      Morphophonological Rules      !!!
!!!                                    !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define SemiAndFinalE [..] -> a || WordBoundary (C) a _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                        e -> 0 ||                    V C _   MorphemeBoundary "~"    ∘  ! final e-dropping
                        e -> 0 ||                        _   MorphemeBoundary  "~" V ∘  ! final e-dropping  
                     [..] -> a || WordBoundary (C) a _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                        e -> 0 ||                    V C _ [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-dropping
                      "~" -> 0 || WordBoundary Alphabet* MorphemeBoundary _          ;  ! finally remove intermediate symbol ~


define SemiFinalE [..] -> a || WordBoundary (C) a _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                     e -> 0 ||                    V C _ [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-dropping
                 "~sf" -> 0 || WordBoundary Alphabet* MorphemeBoundary _          ;  ! finally remove intermediate symbol ~sf 

define FinalE     [..] -> a || WordBoundary (C) a _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                     e -> 0 || WordBoundary Alphabet* V C _ MorphemeBoundary "~f" ∘  ! e-dropping
                     e -> 0 || WordBoundary Alphabet* _ MorphemeBoundary "~f" V   ∘  ! e-dropping
                  "~f" -> 0 || WordBoundary Alphabet* MorphemeBoundary _          ;  ! finally remove intermediate symbol ~f

define NoHopNoDropE "~h" -> 0   || WordBoundary (C) DoubleVowel C e MorphemeBoundary _  ∘  ! does not drop e if e-hopping is not possible
                    "~h" -> 0   || V C V C e MorphemeBoundary _                         ∘  ! does not drop e if base contains multiple syllables
				    "~h" -> "~" || WordBoundary Alphabet* MorphemeBoundary _            ;  ! otherwise drop e

define WeakFinalC  gh   -> 0 || [ WordBoundary | C ] FullVowel _ MorphemeBoundary [("~f") | ("~sf")] "-w"  ∘
                   "-w" -> 0 || WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*)  _               ;

define UnmarkStrongGh gh "*" -> gh ;

define InterConsonantalE "(e)" -> e || C MorphemeBoundary (":") _  ∘  ! convert (e) to e when following a consonant
                         "(e)" -> 0 || V MorphemeBoundary (":") _  ;  ! otherwise delete it

define FinalConsonantDropAssimilation [ gh | g ] -> gh  || gh ("*") MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _   ∘
                                      [ gh | g ] -> g   || g  MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _           ∘
									  ! The line below is unfortunately required because of orthographic undoubling conventions
                                      [ gh | g ] -> wh  || w   MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _  [ p | t | k | kw | q | qw | f | ll | s | wh ] ∘
                                      [ gh | g ] -> w   || w   MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _           ∘
                                      [ gh | g ] -> ghw || ghw MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _           ∘
                                      [  q | k ] -> q   || [ q  | gh  ] MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _  ∘
                                      [  q | k ] -> qw  || [ qw | ghw ] MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _  ∘
                                      [  q | k ] -> kw  || [ kw | w   ] MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _  ∘
                                      [  q | k ] -> k   || [ k  | g   ] MorphemeBoundary [("~") | ("~sf") | ("~f")] "–" _  ∘
                                               k -> q   ||  gh ("*") MorphemeBoundary ("~") ("–") _  ∘  ! k becomes q in the cluster ghk
                                               C -> 0   ||  _ MorphemeBoundary [("~") | ("~sf") | ("~f")] "–"  ∘
									      gh "*" -> 0   ||  _ MorphemeBoundary [("~") | ("~sf") | ("~f")] "–"  ∘
                                             "–" -> 0   ||  WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*) _  ;

define teDropping [..] -> n e gh     || t e MorphemeBoundary "@" MorphPhonSymbols* ng h _   ∘ ! next 2 lines for postbase @–nghite
                  ng h -> 0          || t e MorphemeBoundary "@" MorphPhonSymbols* _ n e gh ∘ 
                  [..] -> nn         || V t e MorphemeBoundary "@" MorphPhonSymbols* _ n  ∘ ! next 3 lines concern postbases that begin with n
                     n -> 0          || V t e MorphemeBoundary "@" MorphPhonSymbols* nn _ ∘
                  [..] -> DoubleFric || C t e MorphemeBoundary "@" MorphPhonSymbols* _ n  ∘
				     C -> 0          || _ t e MorphemeBoundary "@" MorphPhonSymbols* DoubleFric n ∘
				  y   -> s || t e MorphemeBoundary "@" MorphPhonSymbols* _ ∘ ! this line concerns postbases that begin with y
				  e   -> 0 || V t _ MorphemeBoundary "@*" ∘
                  t e -> 0 || _ MorphemeBoundary "@"      ∘
                  "@"  -> 0,
				  "@*" -> 0  || WordBoundary Alphabet* MorphemeBoundary _    ;
 
define JunctureConsonantInsertion ["(g/t)" | "(g)"] -> g || V V MorphemeBoundary [("~f") | ("~")] _ ∘  ! (g) used with bases that end in a double vowel
                                  ["(g/t)" | "(t)"] -> t ||   C MorphemeBoundary ("~f") _ ∘  ! (t) used with bases that end in a consonant
                                  "(ng)" -> "ng"         ||   V MorphemeBoundary [("~")|("~h")] (":") _ ∘  ! (ng) used with bases that end in a vowel 
                                  "(s/z)" -> s || C ("*") MorphemeBoundary _  ∘
                                  "(s/z)" -> z || V       MorphemeBoundary _  ∘                                 
                                  "(p/v)" -> p || C ("*") MorphemeBoundary _  ∘
                                  "(p/v)" -> v || V       MorphemeBoundary _  ∘
                                  "(t/y)" -> t || C ("*") MorphemeBoundary _  ∘
                                  "(t/y)" -> y || V       MorphemeBoundary _  ∘
								  "(pete)fte"  -> p e t e f t e || C ("*") _  ∘
								  "(pete/fte)" -> p e t e MorphemeBoundary || C ("*") _  ∘
								  "(pete/fte)" -> f t e   MorphemeBoundary || V       _  ∘
								  "(g*)"  -> g   || [Alphabet - t] e MorphemeBoundary MorphPhonSymbols* _  ∘
								  "(te)"  -> t e || C ("*") _ ∘
								  "(te)"  -> 0   || V       _ ∘
								  "(i)"   -> i   || [Alphabet - t] e MorphemeBoundary MorphPhonSymbols* _  ∘
    		                      "(g/t)" -> 0,
								  "(g)"   -> 0,
								  "(t)"   -> 0,
								  "(ng)"  -> 0,
								  "(s/z)" -> 0,
								  "(p/v)" -> 0,
								  "(t/y)" -> 0,
								  "(g*)"  -> 0  || WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*) _  ;

define UvularDropping gh MorphemeBoundary ":" e -> a MorphemeBoundary || [ WordBoundary | C ] a _ ∘
                      gh MorphemeBoundary ":" e -> i MorphemeBoundary || [ WordBoundary | C ] i _ ∘
                      gh MorphemeBoundary ":" e -> u MorphemeBoundary || [ WordBoundary | C ] u _ ∘
                      gh MorphemeBoundary ":" a -> a MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                      gh MorphemeBoundary ":" i -> i MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                      gh MorphemeBoundary ":" u -> u MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                                                           ":"   -> 0 || WordBoundary Alphabet* MorphemeBoundary _ ;

define VelarUvularRounding k -> kw, g -> w, gg -> wh, ng -> ngw, ngng -> ngngw || [C|V - u] _ u (MorphemeBoundary) i (MorphemeBoundary)  ∘
                           k -> kw, g -> w, gg -> wh, ng -> ngw, ngng -> ngngw || [C|V - u] _ u (MorphemeBoundary) a (MorphemeBoundary)  ∘
                                     h -> h w, q -> qw, gh -> ghw, ghh -> ghhw || [C|V - u] _ u (MorphemeBoundary) i (MorphemeBoundary)  ∘
                                     h -> h w, q -> qw, gh -> ghw, ghh -> ghhw || [C|V - u] _ u (MorphemeBoundary) a (MorphemeBoundary)  ∘
							                         ["(g/t)" | "(g)"] -> w || a u MorphemeBoundary MorphemeBoundary MorphPhonSymbols* _ ;

define TripleVowelCluster V -> 0 || V _ V ;  ! delete the middle vowel in a cluster of three vowels

define VowelDominance a i -> i i  ∘
                      i a -> i i  ∘
                      u i -> i i  ∘
                      i u -> i i  ∘
                      a u -> a a  ∘
                      u a -> a a  ;

! If we choose this option, reformulate these rules to include relevant symbols in MorphPhonSymbols, and MorphemeBoundaries.
define Optative "+gi"  -> g i     || V V _                  ∘
                "+gi"  -> 0                                 ∘
                "~sfi" -> "~sf" i || [C - gh] _             ∘
                "~sfi" -> "~sf" i || [e | DoubleVowel] gh _ ∘
                "~sfi" -> 0                                 ∘
                "+n"   -> n       || t e _                  ∘
                "+n"   -> 0                                 ∘
                "~fi"  -> "~f" i  || [C - t] e _            ∘
                "~fi"  -> 0                                 ∘
                "x"    -> ":" a    || FullVowel gh _        ∘
                "x"    -> 0                                 ∘
                ! Substituted 'x' to represent the sixth form of the 2nd person intransitive optative ending, since ':a' conflicted with
                ! other tense formations where there was uvular dropping followed by an 'a' - DEVISE BETTER NOTATION
                "~f(i)gu" -> "~f" "(i)" g u  || V _         ∘
                "~f(i)gu" -> 0                              ∘
                "~sf–ggu" -> "~f" "–" ghh u || [q|gh|ghh|qw|ghw|ghhw] _  ∘    ! '~sf' becomes '~f' since the final consonant drops before
                "~sf–ggu" -> "~f" "–" gg u  || C _          ∘                 ! semifinal 'e' is analyzed in the transducer
                "~sf–ggu" -> 0                              ∘
                "(i)" -> i || e ("~f") _                    ∘
				"(i)" -> 0                                  ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          !!!
!!!      Clean-Up Rules      !!!
!!!                          !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define ClusterDevoicing k -> q || gh _ ∘
                        y -> s || _ [Stop | VoicelessFricative | VoicelessNasal] ∘
                        y -> s || [Stop | VoicelessFricative | VoicelessNasal] _ ∘
                        t [z | y] -> s                                           ∘   ! tz/ty cluster becomes s
						z -> s || Stop _                                         ;

define Undoubling ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || _ [Stop | f | s | wh] ∘   
                  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || [Stop | f | s | wh | h] _ ∘   
		          mm -> m, nn -> n, ngng -> ng, ngngw -> ngw        || [Stop | f | s | wh | h] _ ∘
				  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || _ ll ∘ 
                  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || [ll| rr | gg | ghh |ghhw] _ ∘ 
                  mm -> m, nn -> n, ngng -> ng, ngngw -> ngw        || [ll| rr | gg | ghh |ghhw] _ ;

def BaseFinalEndings ghw      -> qw , 
                     gh ("*") -> q  , 
                     g        -> k  ,
                     w        -> kw ,
                     t e      -> t a  ||   _ WordBoundary , 
                     t e      -> n    || V _ WordBoundary ∘
					 e        -> a    ||   _ WordBoundary ∘
				     gh q     -> q    ||   _ WordBoundary ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                        !!!
!!!      Rule Cascade      !!!
!!!                        !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define Rules [
    Lexicon                        ∘
	Lowercase                      ∘
	InsertMorphBoundary            ∘
	!! Iteration 1 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	Optative                       ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 2 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 3 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 4 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 5 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 6 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 7 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	ClusterDevoicing               ∘
	BaseFinalEndings               ∘
	Undoubling
] ;


define Grammar [ Exceptions .P. Rules ] ;
push Grammar

