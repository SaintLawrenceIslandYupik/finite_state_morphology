#!/usr/bin/env foma -f

read lexc exceptions.lexc
def Exceptions

!read lexc tiny.lexc
read lexc ess.lexc
def Lexicon

! define NounLexicon [[Lexicon].i .o. [ ?* "[N]" ?* ] ].i ;
! define VerbLexicon [[Lexicon].i .o. [ ?* "[V]" ?* ] ].i ;


define Stop                [  p   |   t                 |  k     | "kw"   |  q    | "qw"       ] ;
define VoicedFricative     [  v   |   l  | z | y |  r   |  g     |  w     | "gh"  | "ghw"      ] ;
define VoicelessFricative  [  f   | "ll" |   s   | "rr" | "gg"   | "wh"   | "ghh" | "ghhw" | h ] ;
define VoicedNasal         [  m   |   n                 | "ng"   | "ngw"                       ] ;
define VoicelessNasal      [ "mm" | "nn"                | "ngng" | "ngngw"                     ] ;

define Fricative [ VoicelessFricative | VoicedFricative ] ;
define Nasal     [ VoicelessNasal     | VoicedNasal     ] ;

define WordBoundary [ .#. ] ;

define C         [ Stop | Fricative | Nasal ] ;        

define FullVowel   [ a   | i   | u   ] ;
define DoubleVowel [ a a | i i | u u ] ;
define V           [ e | FullVowel ];

define WeakFinalC  gh     -> 0  || [ WordBoundary | C ] FullVowel _ ("~f") "-w"   ∘
                   "-w"   -> 0  ;

define UnmarkStrongGh gh "*" -> gh ;

define FinalE     [..] -> a || WordBoundary (C) a _ C e   "~f"   ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e   "~f"   ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e   "~f"   ∘  ! e-hopping
                     e -> 0 ||                    V C _   "~f"   ∘  ! e-dropping
                     e -> 0 ||                        _   "~f" V ∘  ! e-dropping  (Jacobson, 2001), p.19, footnote 1: 
                                                                    !                 "It is true that all suffixes that begin with a vowel 
                                                                    !                  in the form in which they are used on a base ending in e 
                                                                    !                  will cause that e to drop, but nevertheless the sign ~f 
                                                                    !                  will still be given with such suffixes."
                  "~f" -> 0 ;                                       ! finally remove intermediate symbol ~f

define SemiFinalE [..] -> a || WordBoundary (C) a _ C e C "~sf"  ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e C "~sf"  ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e C "~sf"  ∘  ! e-hopping
                     e -> 0 ||                    V C _ C "~sf"  ∘  ! e-dropping
                 "~sf" -> 0 ;                                       ! finally remove intermediate symbol ~sf

define SemiAndFinalE    
                  [..] -> a || WordBoundary (C) a _ C e   "~"   ∘  ! final e-hopping
                  [..] -> i || WordBoundary (C) i _ C e   "~"   ∘  ! final e-hopping
                  [..] -> u || WordBoundary (C) u _ C e   "~"   ∘  ! final e-hopping
                     e -> 0 ||                    V C _   "~"   ∘  ! final e-dropping
                     e -> 0 ||                        _   "~" V ∘  ! final e-dropping  
                  [..] -> a || WordBoundary (C) a _ C e C "~"   ∘  ! semi-final e-hopping
                  [..] -> i || WordBoundary (C) i _ C e C "~"   ∘  ! semi-final e-hopping
                  [..] -> u || WordBoundary (C) u _ C e C "~"   ∘  ! semi-final e-hopping
                     e -> 0 ||                    V C _ C "~"   ∘  ! semi-final e-dropping
                   "~" -> 0 ;                                      ! finally remove intermediate symbol ~




define UvularDropping gh ":" e -> a || [ WordBoundary | C ] a _    ∘
                      gh ":" e -> i || [ WordBoundary | C ] i _    ∘
                      gh ":" e -> u || [ WordBoundary | C ] u _    ∘
                      gh ":" a -> a || [ WordBoundary | C ] FullVowel _    ∘
                      gh ":" i -> i || [ WordBoundary | C ] FullVowel _    ∘
                      gh ":" u -> u || [ WordBoundary | C ] FullVowel _    ∘
                         ":"   -> 0 ;

define InterConsonantalE "(e)" -> e || C (":") _  ∘                  ! convert (e) to e when following a consonant
                         "(e)" -> 0 || V (":") _  ;                  ! otherwise delete it

! Jacobson (2001, p.12) states: "(Noun) Bases  can end only in a vowel ... or in one of the consonants gh, g, ghw, or w."
! Jacobson (2001, p.18) states: "Verb bases end in precisely the same ways that noun bases do, that is,
!                                in a full vowel, in e, in te, or in one of the fricatives, gh, g, ghw, or w."
! 
! For the purposes of this rule, an interesting research question might be to elicit data
!     where a made-up base ending in something else (for example ngw),
!     to see how this affects this rule.
! 
! Jacobson (2001, p.24) states: "Likewise, it will substitute a rounded fricative, wh or (voiceless) ghw, 
!                                if the base ends in a rounded velar or uvular fricative."
! 
! However, in the succeeding example on the same page, the base kiiw- goes to kiiwrugllagek.
! That would seem to indicate that the voiced consonants w is to be substituted, and not wh.
! Additionally, ghw is voiced, not voiceless.
! Overall it seems that the above quotation contains mistakes.
! 
! Additionally, Jacobson (2001, p.24) is unclear regard how assimilation affects postbases ending in q and k.
! Does assimilation occur only in place?   (k -> q  || Uvular  _       and q -> k  || Velar   _ )
! Or does assimilation occur in rounding?  (k -> kw || Rounded _       and q -> qw || Rounded _ )
! Or both?                                 (k -> qw || RoundedUvular _ and q -> kw || RoundedVelar _ )
! 
! This probably requires elicitation to determine.
! 
! Oh, my. Looks like this is partially a problem with the orthography.
! In the example on p.24 the postbase is –ghllak. This means that the gh in the postbase is actually voiceless ghh.
! That explains the (voiceless) bit in the quotation up above.
! 
! Also, there is a postbase ~sf–gga that begins with –gg. What happens to it? 
! Does it become ghhw or ghh in the presence of a (rounded) uvular?
! 
define FinalConsonantDropAssimilation [ gh | g ] -> gh  || gh  "–" _  ∘
                                      [ gh | g ] -> g   || g   "–" _  ∘
                                      [ gh | g ] -> wh  || w   "–" _  [ p | t | k | kw | q | qw | f | ll | s | wh ] ∘ ! This line is unfortunately required because of orthographic undoubling conventions
                                      [ gh | g ] -> w   || w   "–" _  ∘
                                      [ gh | g ] -> ghw || ghw "–" _  ∘
                                      [  q | k ] -> q   || [ q  | gh  ] "–" _  ∘
                                      [  q | k ] -> qw  || [ qw | ghw ] "–" _  ∘
                                      [  q | k ] -> kw  || [ kw | w   ] "–" _  ∘
                                      [  q | k ] -> k   || [ k  | g   ] "–" _  ∘
                                               C -> 0   ||            _ "–"    ∘
                                             "–" -> 0                          ;

!define FinalConsonantDropAssimilation gh  "–" [ gh | g ] -> gh  ∘
!                                      g   "–" [ gh | g ] -> g   ∘
!                                      w   "–" [ gh | g ] -> wh || _ [ p | t | k | kw | q | qw | f | ll | s | wh ] ∘ ! This line is unfortunately required because of orthographic undoubling conventions
!                                      w   "–" [ gh | g ] -> w   ∘
!                                      ghw "–" [ gh | g ] -> ghw ∘
!                                      q   "–" [  q | k ] -> q   ∘
!                                      qw  "–" [  q | k ] -> qw  ∘
!                                      k   "–" [  q | k ] -> k   ∘
!                                      kw  "–" [  q | k ] -> kw  ∘
!                                      C   "–"            -> 0   ∘
!                                          "–"            -> 0   ;


!define FinalConsonantDropAssimilation gh  "–" [ gh | g | k | q ] -> gh  ∘
!                                      g   "–" [ gh | g | k | q ] -> g   ∘
!                                      k   "–" [ gh | g | k | q ] -> k   ∘
!                                      q   "–" [ gh | g | k | q ] -> q   ∘
!                                      w   "–" [ gh | g | k | q ] -> w   ∘
!                                      ghw "–" [ gh | g | k | q ] -> ghw ∘
!                                      C   "–"                    -> 0   ∘
!                                          "–"                    -> 0   ;


def BaseFinalEndings    ghw      -> qw , 
                        gh ("*") -> q  , 
                        g        -> k  ,
                        w        -> kw ,
                        t e      -> t a  ||   _ WordBoundary  ,, 
                        t e      -> n    || V _ WordBoundary   ∘
                        e        -> a    ||   _ WordBoundary ;

define AbsolutiveConsonantChange "(s/z)" -> s || C _   ∘
                                 "(s/z)" -> z || V _   ∘                                 
                                 "(p/v)" -> p || C _   ∘
                                 "(p/v)" -> v || V _   ∘
                                 "(t/y)" -> t || C _   ∘
                                 "(t/y)" -> y || V _   ;
                                 



define IntrInd "(g/t)" -> g || V V ("~f") _    ∘
               "(g/t)" -> t ||   C ("~f") _    ∘
               "(g/t)" -> 0 ;

define VowelDominance a i -> i i  ∘
                      i a -> i i  ∘
                      u i -> i i  ∘
                      i u -> i i  ∘
                      a u -> a a  ∘
                      u a -> a a  ;

define NasalInsertion "(ng)" -> "ng" || V "~" ":" _  ∘
                      "(ng)" -> 0 ;

!define Nouns  [ 
!		NounLexicon ∘
!		SemiAndFinalE ∘ 
!		SemiFinalE  ∘ 
!		FinalE      ∘ 
!		WeakFinalC  ∘ 
!		UnmarkStrongGh ∘ 
!		InterConsonantalE ∘ 
!		UvularDropping ∘ 
!		FinalConsonantDropAssimilation ∘
!		AbsolutiveConsonantChange ∘
!		BaseFinalEndings 
!	];
! 
!define Verbs [ 
!		VerbLexicon    ∘ 
!		UnmarkStrongGh ∘ 
!		NasalInsertion ∘ 
!		SemiAndFinalE  ∘ 
!		UvularDropping ∘ 
!		IntrInd        ∘ 
!		FinalE         ∘ 
!		VowelDominance 
!	] ;
! 
!define Lexicon  Exceptions .P. [ Nouns |  Verbs ] ;

define Rules [
	Lexicon ∘
	FinalConsonantDropAssimilation ∘
	AbsolutiveConsonantChange ∘
	SemiAndFinalE ∘ 
	SemiFinalE  ∘ 
	WeakFinalC  ∘ 
	UnmarkStrongGh ∘
	NasalInsertion ∘ 
	InterConsonantalE ∘ 
	UvularDropping ∘ 
	IntrInd        ∘ 
	FinalE      ∘ 
	VowelDominance ∘
	BaseFinalEndings 

];

define Grammar [ Exceptions .P. Rules ] ;
push Grammar



! aghnagh~:(ng)u~f(g/t)u q
!           ⇓                nasal insertion
! aghnagh~:    u~f(g/t)u q
!        ⇓                   final and semi-final e dropping
! aghna   :    u~f(g/t)u q
!         ⇓                  uvular dropping
! aghna        u  (g/t)u q
!                  ⇓         (g/t) IntrInd
! aghna        u   g   u q
!              ⇓             vowel dominance
! aghna        a   g   u q
!                            surface form
! aghnaaguq
! 
! This example shows that "(g/t) IntrInd" must happen *after* "uvular dropping"


! megh~f(g/t)u q
!     ⇓                     final e dropping
! megh  (g/t)u q
!        ⇓                  (g/t) IntrInd             
! megh     t u q
!                            surface form
! meghtuq


! taake~f(g/t)u q
!           ⇓               (g/t) IntrInd             
! taake~f     u q
!      ⇓                    final e dropping
! taak        u q
!                            surface form
! taakuq


! aghnagh~f-wmeng
!         ⇓                 final e dropping
! aghnagh  -wmeng
!           ⇓               weak final c dropping
! aghna      meng
!                            surface form
! aghnameng


! ape–leg~:(ng)u~f(g/t)u q
!        ⇓                    final and semifinal e dropping 
! ape–l g~:(ng)u~f(g/t)u q

