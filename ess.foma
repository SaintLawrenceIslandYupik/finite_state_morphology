#!/usr/bin/env foma -f

read lexc exceptions.lexc
define Exceptions

read lexc ess.lexc
define Lexicon


!==============
! MISC SYMBOLS 
!==============
! "^"    morpheme boundary
! .#.    word boundary
! %{g%} h  weak gh


!======
! SETS 
!======

define C          [ f | g | h | k | l | m |
                    n | p | q | r | s | t |
                    v | w | y | z |
    			    %{t%} | %{g%} | %{k%} ] ;

define Stop       [ p   | t   | k   | q   |
                    k w | q w ];

define VoicedC    [ g h w | g h |
                    v | l | z   | y | r | g | w |
                    m | n | n g | n g w ] ;

define UnvoicedC  [ p   | t   | k   | q   |
                    k w | q w |
                    f   | s   | h   |
                    l l | r r | g g | w h | g h h | g h h w | 
                    m m | n n | n g n g   | n g n g w ] ;

define MultiCharC [ n g n g w |
                    n g n g   | g h h w | g h h |
                    g h w     | n g w   |
                    g g       | g h     | k w   | l l | m m |
                    n g       | n n     | r r   | q w | w h ];

define FullVowel  [ a | i | u ]     ;
define V          [ e | FullVowel ] ;

define Allomorph  [ %{E%} | %{I%} | %{GI%} | %{AI%} ] ;


!=======
! RULES
!=======

define SemiFinalE [..] -> e || .#. (C) (C) FullVowel _ C e g (h) "^" %{%.sf%.%} .o. ! hop semi-final -e 
                     e -> 0 ||           [ ? - C ] C _ g (h) "^" %{%.sf%.%}     .o. ! -e cannot drop if it leads to a 3-consonant cluster
            %{%.sf%.%} -> 0                                                     ;

define FinalE %{%.f%.%} -> 0 || [ (C) C C & ~MultiCharC ] e "^" _ [ MultiCharC | C ] .o. ! do not hop or drop final e if doing so results
                                                                                         ! in a cluster of three consonants e.g.
                                                                                         ! riigte[Abl.Mod][Sg] -> riigtmeng
                                                                                         ! (cf. yughaghte[Abs][Sg][3SgPoss] -> yughaghtenga)
                                                                                         ! [Jacobson (2001) p.21]
                  [..] -> e || .#. ( MultiCharC | C ) FullVowel _ C e  "^" %{%.f%.%} .o. ! hop final -e
                     e -> 0 || [ V | "^"] ([ MultiCharC | C ]) [ MultiCharC | C ] _ "^" %{%.f%.%} .o. ! VowelDominance will apply later             
               %{%.f%.%} -> 0                                                        ;


define LongRootFinalE e -> 0 || _ "^" %{%.fL%.%} .o. ! drop final -e but do not hop if root exceeds two syllables [Jacobson (2001) p.32]
             %{%.fL%.%} -> 0                     ;


define DropFinalC g h -> g     || g               "^" %{%.m%.%} _ [ ? - .#. ]       .o. ! assimilate gh- onset to dropped -g
                  g h -> g h w || g h w           "^" %{%.m%.%} _ [ ? - .#. ]       .o. ! assimilate gh- onset to dropped -ghw
                  g h -> w h   || %{k%} w         "^" %{%.m%.%} _ UnvoicedC         .o. ! assimilate gh- onset to dropped -w
                  g h -> w     || %{k%} w         "^" %{%.m%.%} _ VoicedC           .o. ! assimilate gh- onset to dropped -w
                    g -> g h   || [ g | %{g%} ] h "^" %{%.m%.%} _ [ V | C - g - h ] .o. ! assimilate g- onset to dropped -gh
                    k -> q w   || g h w           "^" %{%.m%.%} _ [ ? - .#. ]       .o. ! assimilate k-  onset to dropped -ghw 
                    k -> q     || [ g | %{g%} ] h "^" %{%.m%.%} _ [ ? - .#. ]       .o. ! assimilate k-  onset to dropped -gh
                    k -> k w   || w               "^" %{%.m%.%} _ [ ? - .#. ]       .o. ! assimilate k-  onset to dropped -w
                  g g -> g h h ||             g h "^" %{%.m%.%} _                   .o. ! assimilate gg- onset to dropped -gh [Jacobson (2001) p.66]
  [ ~MultiCharC & C ] -> 0     ||                               _ "^" %{%.m%.%}     .o. ! drop root-final consonant
  [ g | %{g%} ] ( h ) -> 0     ||                               _ "^" %{%.m%.%}     .o. ! drop -g in root-final -gh, -gh in -ghw
           MultiCharC -> 0     ||                               _ "^" %{%.m%.%}     .o. ! drop root-final consonant
            %{%.m%.%} -> 0                                                          ;


define DropWeakC %{g%} h -> 0 || _ "^" %{%.w%.%} .o. ! Drop a weak gh from the base
               %{%.w%.%} -> 0                    ;


define DropUvular %{g%} h -> 0   || C FullVowel _ "^" %{%.c%.%} [ FullVowel | Allomorph ] .o. ! drop uvular -gh between two single vowels
                      g h -> 0   || C FullVowel _ "^" %{%.c%.%} [ FullVowel | Allomorph ] .o. ! where the second vowel can be a full vowel
                %{%.c%.%} -> 0                                                            ;   ! or an allomorph {E}


define VowelDominance a i -> i i  .o.
                      i a -> i i  .o.
                      u i -> i i  .o.
                      i u -> i i  .o. 
                      a u -> a a  .o. 
                      u a -> a a  .o.
                      i e -> i i  .o. ! e hopping
                      a e -> a a  .o.
                      u e -> u u  ;


define VelarUvularRounding n g n g -> n g n g w || [ C | V - u ] _ u [ i | a ] .o. ! velar/uvular rounding [Jacobson (2001) p.36]
                               n g -> n g w     || [ C | V - u ] _ u [ i | a ] .o. ! needs to occur before VowelDominance
                             g h h -> g h h w   || [ C | V - u ] _ u [ i | a ] .o. ! conditions should apply to the longest match
                               g h -> g h w     || [ C | V - u ] _ u [ i | a ] .o.   
                               g g -> w h       || [ C | V - u ] _ u [ i | a ] .o.   
                                 k -> k w       || [ C | V - u ] _ u [ i | a ] .o.   
                                 g -> w         || [ C | V - u ] _ u [ i | a ] .o.  
                                 q -> q w       || [ C | V - u ] _ u [ i | a ] ; 


define ShortBaseFinalE e -> e || .#. C (C) V C (C) _ "^" (%{%.f%.%}) n g a , ! [Jacobson (2001) p.38]
                       e -> a || .#. C (C) V C (C) _ "^" (%{%.f%.%}) n g a ; ! needs to occur before e dropping


define DeleteEBeforeVowel e -> 0 || _ "^" a , ! not sure where this rule is stated, but [Intr][Intrg][1Pl]:%^ste%^a -> %^st%^a
                          e -> 0 || _ "^" i ; ! [Intr][Opt][2Pl/Du] drop final -e, e.g. aanitek but there is no ~f in these morphemes


!-------------------
! ALLOMORPHY RULES
!-------------------

define AddE %{E%} -> a || a "^" _ .o.
            %{E%} -> i || i "^" _ .o.
            %{E%} -> u || u "^" _ .o.
            %{E%} -> e || C "^" _ .o.
            %{E%} -> 0            ;


define ResolveG %{G%} -> w || u "^" _ [ a | i ] ("^") [ C | .#. ] .o. ! velar rounding [Jacobson (2001) p.30]
                                                                      ! do not round if DropMiddleVowel can occur [Jacobson (2001) p.44]
                %{G%} -> g || FullVowel ("^") FullVowel "^" _     .o.
                %{G%} -> 0                                        ;


define ResolveI %{I%} -> e ||       g h "^" _ .o. ! for postbase ~sf:(e)sqe [Jacobson p.64]
                %{I%} -> i || FullVowel "^" _ .o.
                %{I%} -> 0                    ;


define OptativeAllomorphs %{GI%} -> g i || FullVowel ("^") FullVowel "^" _ .o. ! -a, -i, -u vs. -aa, -ii, -uu [Jacobson p.65]
                          %{AI%} -> i   ||                       g h "^" _ .o. ! -agh, -igh, -ugh vs. -aagh, -iigh, -uugh [Jacobson p.65]
                          %{AI%} -> a   ||                 FullVowel "^" _ .o.
                          %{GI%} -> 0                                      ;


!------------------
! MODIFY -TE RULES
!------------------
    
define DevoiceApicalTE y   -> s,
                       n   -> n n   || %{t%} e "^" %{%.at%.%} _                       .o.
                       g   -> g g                                                     ,   ! the only consonants before -te that double
                       l   -> l l                                                     ,   ! to show voicelessness are -g, -l, -gh
                       g h -> g h h || _ %{t%} e "^" %{%.at%.%} n                     .o. ! [Jacobson (2001) p.31]
                       n   -> 0     || [ g g | l l | g h h ] %{t%} e "^" %{%.at%.%} _ ;   ! undouble the -n according to undoubling rules


define DoNotDropTE  e -> 0 , e -> e ||       V %{t%} _ "^" %{%.at%.%} k a g h .o. ! specific to -@~sf–(g)kagh[V.N] as in tuqute-@~sf–(g)kagh[V.N][Abs][Sg] 
         %{t%} -> %{t%}, %{t%} -> s ||             V _ "^" %{%.at%.%} k a g h .o. ! tuqutkaq, tuquskaq [Jacobson (2001) p.58]
                    %{%.at%.%} -> g || V %{t%} e "^" _ k a g h                .o. ! tuqutegkaq
                    %{%.at%.%} -> 0 || C %{t%} e "^" _ k a g h                ;   ! e.g. ingaghte-@~sf–(g)kagh[V.N][Abs][Sg]  ingaghtekaq


define DropTE %{t%} e -> 0 || _ "^" %{%.at%.%} .o.
           %{%.at%.%} -> 0                     ; 


!----------------
! CLEAN-UP RULES
!----------------

define MatchVoicing y -> s ||            _ [ k | q | g h h ] .o. ! [Jacobson (2001) p.66] e.g. paasghu
                    y -> s ||  [ k | q ] _                   .o.
                    k -> q ||        g h _                   .o.
                    z -> s || Stop ("^") _                   .o. ! [Jacobson (2001) p.51] e.g. qiyaaqsin
                    v -> f || _ ("^") Stop                   ;   ! [Jacobson (2001) p.58] e.g. kuufkaq


define BaseFinalEndings %{t%} e -> n   ||               V _     "^" .#. ,   ! Jacobson 2.2.1 Words and Bases
                        %{t%} e -> t a                                  .o. 
                              e -> a   ||                 _     "^" .#. .o. 
                              w -> k w || [C - h | %{k%}] _     "^" .#. .o. 
                            g h -> q   ||                 _ (w) "^" .#. .o.
                        %{g%} h -> q   ||                 _     "^" .#. .o.  
                              g -> k   ||                 _     "^" .#. .o.
          [ %{t%} | t ] ("^") z -> s                                    ; ! [Jacobson (2001) p.51] e.g. ~f(t)zin


define PreventGemination n -> 0 || _ "^" n ; ! is this the only instance of accidental gemination? [Jacobson (2001) p.31]


define CleanupDigraph %{k%}   -> 0   .o. 
                      %{g%} h -> g h .o.
                      %{t%}   -> t   ;


define CleanupMorphBoundary "^" -> 0 ;


define DropMiddleVowel V -> 0 || V _ V ;


! TODO: this will have to be fixed since we aren't treating the undoubled graphemes as multichar anymore...
define Undoubling l l -> l, r r -> r, g g -> g, g h h -> g h, g h h w -> g h w || _ [Stop | f | s]     .o.
                  l l -> l, r r -> r, g g -> g, g h h -> g h, g h h w -> g h w || [Stop | f | s | h] _ .o.
				  m m -> m, n n -> n, n g n g -> n g, n g n g w -> n g w        || [Stop | f | s | h] _ .o.
				  l l -> l, r r -> r, g g -> g, g h h -> g h, g h h w -> g h w || _ l l .o.
		          l l -> l, r r -> r, g g -> g, g h h -> g h, g h h w -> g h w || [l l| r r | g g | g h h | g h h w] _ .o.
				  m m -> m, n n -> n, n g n g -> n g, n g n g w -> n g w        || [l l| r r | g g | g h h |g h h w] _ ;


!==============
! RULE CASCADE
!==============

define Rules [
    SemiFinalE           .o.
    LongRootFinalE       .o.
    DevoiceApicalTE      .o.
    DoNotDropTE          .o.
    DropTE               .o.
    DropFinalC           .o.
    DropWeakC            .o.
    DropUvular           .o.
    ShortBaseFinalE      .o. 
    FinalE               .o.
    AddE                 .o.
    DeleteEBeforeVowel   .o.
    ResolveG             .o.
	ResolveI             .o.
	OptativeAllomorphs   .o.
    BaseFinalEndings     .o. 
    PreventGemination    .o.
    CleanupDigraph       .o.
    CleanupMorphBoundary .o.
    DropMiddleVowel      .o.
    VelarUvularRounding  .o.
    VowelDominance       .o.
    MatchVoicing         
!	Undoubling
];

define Grammar [ [ Exceptions .o. Rules ] .P. [ Lexicon .o. Rules ] ] ;
