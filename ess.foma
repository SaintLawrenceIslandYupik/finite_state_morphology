#!/usr/bin/env foma -f

read lexc exceptions.lexc
def Exceptions

read lexc ess.lexc

eliminate flag VALENCE
define LexiconNoFlags ;
define Lexicon LexiconNoFlags ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                                       !!!
!!!      Phoneme/Grapheme Definition      !!!
!!!                                       !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define WordBoundary [ .#. ] ;
define MorphemeBoundary "^" ;


define Stop               [  p   |   t                 |  k     | "kw"   |  q    | "qw"       ] ;
define VoicedFricative    [  v   |   l  | z | y |  r   |  g     |  w     | "gh"  | "ghw"      ] ;
define VoicelessFricative [  f   | "ll" |   s   | "rr" | "gg"   | "wh"   | "ghh" | "ghhw" | h ] ;
define VoicedNasal        [  m   |   n                 | "ng"   | "ngw"                       ] ;
define VoicelessNasal     [ "mm" | "nn"                | "ngng" | "ngngw"                     ] ;

define Fricative [ VoicelessFricative | VoicedFricative ] ;
define Nasal     [ VoicelessNasal     | VoicedNasal     ] ;
define DoubleFric [ "ll" | "rr" | "gg" | "ghh" | "ghhw" ] ;

define C         [ Stop | Fricative | Nasal ] ;        

define FullVowel   [ a   | i   | u   ] ;
define DoubleVowel [ a a | i i | u u ] ;
define V           [ e | FullVowel ];

define Alphabet [ C | V | "*" ] ;
define MorphPhonSymbols [ "~"     | "~f"   | "~sf"   | "~h"    | "-w"    | ":"     | "–" |
                          "@*"    | "@₁"   | "@₂"    | "@₃"    |
                          "(e)"   | "(g₁)" | "(t)"   | "(g₂)"  | "(te)"  | "(i₁)"  | "(i₂)" | "(u)" |
						  "(g/t)" | "(ng)" | "(p/v)" | "(s/z)" | "(t/y)" | "(u/i)" ] ;

define Lowercase "A" -> a ,
	             "I" -> i ,
				 "U" -> u ,
				 "E" -> e ,
				 "P" -> p ,
				 "T" -> t ,
				 "K" -> k ,
				 "Kw" -> kw ,
				 "Q" -> q ,
				 "Qw" -> qw,
				 "V" -> v ,
				 "L" -> l ,
				 "Z" -> z ,
                 "Y" -> y , 
                 "R" -> r ,
				 "G" -> g ,
				 "W" -> w ,
				 "Gh" -> gh ,
				 "Ghw" -> ghw ,
				 "F" -> f ,
				 "Ll" -> ll ,
				 "S" -> s ,
				 "Rr" -> rr ,
				 "Gg" -> gg ,
				 "Wh" -> wh ,
				 "Ghh" -> ghh ,
				 "Ghhw" -> ghhw ,
                 "M" -> m ,
				 "N" -> n ,
				 "Ng" -> ng ,
				 "Ngw" -> ngw ,
				 "Mm" -> mm ,
				 "Nn" -> nn ,
				 "Ngng" -> ngng ,
				 "Ngngw" -> ngngw ;

define InsertMorphBoundary [..] -> MorphemeBoundary || Alphabet _ MorphPhonSymbols ;

define CleanupMorphBoundary MorphemeBoundary -> 0 || WordBoundary Alphabet* _ ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                                    !!!
!!!      Morphophonological Rules      !!!
!!!                                    !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define SemiAndFinalE [..] -> a || WordBoundary (C) a _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e   MorphemeBoundary "~"    ∘  ! final e-hopping
                        e -> 0 ||                    V C _   MorphemeBoundary "~"    ∘  ! final e-dropping
                        e -> 0 ||    _   MorphemeBoundary  "~" (MorphPhonSymbols*) V ∘  ! final e-dropping  
                     [..] -> a || WordBoundary (C) a _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                     [..] -> i || WordBoundary (C) i _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                     [..] -> u || WordBoundary (C) u _ C e [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-hopping
                        e -> 0 ||                    V C _ [g | gh ("*")] MorphemeBoundary "~"    ∘  ! semi-final e-dropping
                      "~" -> 0 || WordBoundary Alphabet* MorphemeBoundary _                       ;  ! finally remove intermediate symbol ~


define SemiFinalE [..] -> a || WordBoundary (C) a _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-hopping
                     e -> 0 ||                    V C _ [g | gh ("*")] MorphemeBoundary "~sf"  ∘  ! e-dropping
                 "~sf" -> 0 || WordBoundary Alphabet* MorphemeBoundary _          ;  ! finally remove intermediate symbol ~sf 

define FinalE     [..] -> a || WordBoundary (C) a _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                  [..] -> i || WordBoundary (C) i _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                  [..] -> u || WordBoundary (C) u _ C e MorphemeBoundary "~f"     ∘  ! e-hopping
                     e -> 0 || WordBoundary Alphabet* V C _ MorphemeBoundary "~f" ∘  ! e-dropping
                     e -> 0 || WordBoundary Alphabet* _ MorphemeBoundary "~f" V   ∘  ! e-dropping
                  "~f" -> 0 || WordBoundary Alphabet* MorphemeBoundary _          ;  ! finally remove intermediate symbol ~f

define NoHopNoDropE "~h" -> 0   || WordBoundary (C) DoubleVowel C e MorphemeBoundary _  ∘  ! does not drop e if e-hopping is not possible
                    "~h" -> 0   || V C V C e MorphemeBoundary _                         ∘  ! does not drop e if base contains multiple syllables
				    "~h" -> "~" || WordBoundary Alphabet* MorphemeBoundary _            ;  ! otherwise drop e

define InterConsonantalE "(e)" -> e || C MorphemeBoundary (":") _  ∘  ! convert (e) to e when following a consonant
                         "(e)" -> 0 || V MorphemeBoundary (":") _  ;  ! otherwise delete it
						 
define WeakFinalC  gh   -> 0 || [ WordBoundary | C ] FullVowel _ MorphemeBoundary [("~f") | ("~sf")] "-w"  ∘
                   "-w" -> 0 || WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*)  _               ;

define UnmarkStrongGh gh "*" -> gh ;

define FinalConsonantDropAssimilation [ gh | g ] -> gh  || gh ("*") MorphemeBoundary ("~f") "–" _   ∘
                                      [ gh | g ] -> g   || g  MorphemeBoundary       ("~f") "–" _   ∘
									  ! The line below is unfortunately required because of orthographic undoubling conventions
                                      [ gh | g ] -> wh  || w   MorphemeBoundary ("~f") "–" _  [ p | t | k | kw | q | qw | f | ll | s | wh ] ∘
                                      [ gh | g ] -> w   || w   MorphemeBoundary ("~f") "–" _        ∘
                                      [ gh | g ] -> ghw || ghw MorphemeBoundary ("~f") "–" _        ∘
                                      [  q | k ] -> q   || [ q  | gh  ] MorphemeBoundary ("~f") "–" _  ∘
                                      [  q | k ] -> qw  || [ qw | ghw ] MorphemeBoundary ("~f") "–" _  ∘
                                      [  q | k ] -> kw  || [ kw | w   ] MorphemeBoundary ("~f") "–" _  ∘
                                      [  q | k ] -> k   || [ k  | g   ] MorphemeBoundary ("~f") "–" _  ∘
                                               k -> q   ||  gh ("*") MorphemeBoundary ("–") _  ∘  ! k becomes q in the cluster ghk
                                               C -> 0   ||  _ MorphemeBoundary ("~f") "–"  ∘
									      gh "*" -> 0   ||  _ MorphemeBoundary ("~f") "–"  ∘
                                             "–" -> 0   ||  WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*) _  ;

define teDropping "–"  -> 0 || WordBoundary Alphabet* (t) e MorphemeBoundary MorphPhonSymbols* _   ∘
                  [..] -> n e gh     || t e MorphemeBoundary "@₁" MorphPhonSymbols* ng h _         ∘ ! for postbase @–nghite
                  ng h -> 0          || t e MorphemeBoundary "@₁" MorphPhonSymbols* _ n e gh       ∘ 
                  [..] -> nn         || V t e MorphemeBoundary "@₁" MorphPhonSymbols* _ n          ∘ ! for postbases that begin with n
                     n -> 0          || V t e MorphemeBoundary "@₁" MorphPhonSymbols* nn _         ∘
                  [..] -> DoubleFric || C t e MorphemeBoundary "@₁" MorphPhonSymbols* _ n          ∘
				     C -> 0          || _ t e MorphemeBoundary "@₁" MorphPhonSymbols* DoubleFric n ∘
				  y    -> s || t e MorphemeBoundary "@₁" MorphPhonSymbols* _                       ∘ ! for postbases that begin with y
				  e    -> 0 || V t _ MorphemeBoundary MorphPhonSymbols* "@₂"                       ∘
				  t    -> s || _ (e) MorphemeBoundary MorphPhonSymbols* "@₃"                       ∘ 
				  l gh -> ll ghh || t e MorphemeBoundary "@₁" _                                    ∘ ! for postbase @–lghii
                  [..] -> e || C t e MorphemeBoundary "@₁" ll _ ghh                                ∘
				  t e "*" -> l || _ MorphemeBoundary "@*"                                          ∘ ! for special te's
                  t e  -> 0 || _ MorphemeBoundary MorphPhonSymbols* "@₁"                           ∘
				  "@*" -> 0,
                  "@₁" -> 0,
				  "@₂" -> 0,
				  "@₃" -> 0 || WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*)  _      ∘
				  "*"  -> 0 || WordBoundary Alphabet* t e _ MorphemeBoundary                       ; 
 
define JunctureConsonantInsertion ["(g/t)" | "(g₁)"] -> g || V V MorphemeBoundary [("~f") | ("~")] _       ∘
                                  ["(g/t)" | "(t)"] -> t ||   C MorphemeBoundary ("~f") _                  ∘
                                  "(ng)"  -> "ng"         ||   V MorphemeBoundary [("~")|("~h")] (":") _   ∘
                                  "(s/z)" -> s || C ("*") MorphemeBoundary _  ∘
                                  "(s/z)" -> z || V       MorphemeBoundary _  ∘                                 
                                  "(p/v)" -> p || C ("*") MorphemeBoundary _  ∘
                                  "(p/v)" -> v || V       MorphemeBoundary _  ∘
                                  "(t/y)" -> t || C ("*") MorphemeBoundary _  ∘
                                  "(t/y)" -> y || V       MorphemeBoundary _  ∘
								  "(pete)fte"  -> p e t e f t e || C ("*") _  ∘
								  "(pete/fte)" -> p e t e MorphemeBoundary || C ("*") _  ∘
								  "(pete/fte)" -> f t e   MorphemeBoundary || V       _  ∘
								  "(g₂)"  -> g   || [Alphabet - t] e MorphemeBoundary MorphPhonSymbols* _  ∘
								  "(te)"  -> t e || C ("*") _ ∘
								  "(te)"  -> 0   || V       _ ∘
								  "(i₁)"  -> i   || e MorphemeBoundary MorphPhonSymbols* _                 ∘
								  "(i₂)"  -> i   || [Alphabet - t] e MorphemeBoundary MorphPhonSymbols* _  ∘
								  "(u/i)" -> u   || [Alphabet - t] e MorphemeBoundary MorphPhonSymbols* _  ∘
								  "(u/i)" -> u   || [Alphabet - FullVowel] MorphemeBoundary MorphPhonSymbols* _  ∘
								  "(u/i)" -> i   || t e MorphemeBoundary MorphPhonSymbols* _               ∘
								  "(u)"   -> u   || [Alphabet - FullVowel] MorphemeBoundary MorphPhonSymbols* _  ∘
    		                      "(g/t)" -> 0,
								  "(g₁)"  -> 0,
								  "(t)"   -> 0,
								  "(ng)"  -> 0,
								  "(s/z)" -> 0,
								  "(p/v)" -> 0,
								  "(t/y)" -> 0,
								  "(g₂)"  -> 0,
								  "(i₁)"  -> 0,
								  "(i₂)"  -> 0,
								  "(u/i)" -> 0,
								  "(u)"   -> 0  || WordBoundary Alphabet* MorphemeBoundary (MorphPhonSymbols*) _  ;

define UvularDropping gh MorphemeBoundary ":" e -> a MorphemeBoundary || [ WordBoundary | C ] a _ ∘
                      gh MorphemeBoundary ":" e -> i MorphemeBoundary || [ WordBoundary | C ] i _ ∘
                      gh MorphemeBoundary ":" e -> u MorphemeBoundary || [ WordBoundary | C ] u _ ∘
                      gh MorphemeBoundary ":" a -> a MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                      gh MorphemeBoundary ":" i -> i MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                      gh MorphemeBoundary ":" u -> u MorphemeBoundary || [ WordBoundary | C ] FullVowel _ ∘
                                                           ":"   -> 0 || WordBoundary Alphabet* MorphemeBoundary _ ;

define VelarUvularRounding k -> kw, g -> w, gg -> wh, ng -> ngw, ngng -> ngngw || [C|V - u] _ u (MorphemeBoundary) i (MorphemeBoundary)  ∘
                           k -> kw, g -> w, gg -> wh, ng -> ngw, ngng -> ngngw || [C|V - u] _ u (MorphemeBoundary) a (MorphemeBoundary)  ∘
                                     h -> h w, q -> qw, gh -> ghw, ghh -> ghhw || [C|V - u] _ u (MorphemeBoundary) i (MorphemeBoundary)  ∘
                                     h -> h w, q -> qw, gh -> ghw, ghh -> ghhw || [C|V - u] _ u (MorphemeBoundary) a (MorphemeBoundary)  ∘
							                         ["(g/t)" | "(g)"] -> w || a u MorphemeBoundary MorphemeBoundary MorphPhonSymbols* _ ;

define TripleVowelCluster V -> 0 || V _ V ;  ! delete the middle vowel in a cluster of three vowels

define VowelDominance a i -> i i  ∘
                      i a -> i i  ∘
                      u i -> i i  ∘
                      i u -> i i  ∘
                      a u -> a a  ∘
                      u a -> a a  ∘
					  e i -> i    ;

define Optative [..] -> MorphemeBoundary   || _ "[Opt_PRS_2Sg]"                        ∘
                [..] -> MorphemeBoundary   || _ "[Opt_PRS_2Sg_3Sg]"                    ∘
                "[Opt_PRS_2Sg]" -> g i     || V V MorphemeBoundary _                   ∘
                "[Opt_PRS_2Sg]" -> "~sf" i || [C - gh] MorphemeBoundary _              ∘
                "[Opt_PRS_2Sg]" -> "~sf" i || [e | DoubleVowel] gh MorphemeBoundary _  ∘
                "[Opt_PRS_2Sg]" -> n       || t e MorphemeBoundary _                   ∘
                "[Opt_PRS_2Sg]" -> "~f" i  || [C - t] e MorphemeBoundary _             ∘
                "[Opt_PRS_2Sg]" -> ":" a   || FullVowel gh MorphemeBoundary _          ∘
				"[Opt_PRS_2Sg]" -> 0                                                   ∘
                "[Opt_PRS_2Sg_3Sg]" -> "~f" "(i₁)" g u  || V MorphemeBoundary _        ∘
                "[Opt_PRS_2Sg_3Sg]" -> "~sf" "–" ghh u || [ q | gh | ghh | qw | ghw | ghhw ] MorphemeBoundary _  ∘
                "[Opt_PRS_2Sg_3Sg]" -> "~sf" "–" gg u  || C MorphemeBoundary _         ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                          !!!
!!!      Clean-Up Rules      !!!
!!!                          !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define ClusterDevoicing k -> q || gh _ ∘
                        y -> s || _ [Stop | VoicelessFricative | VoicelessNasal] ∘
                        y -> s || [Stop | VoicelessFricative | VoicelessNasal] _ ∘
                        t [z | y] -> s                                           ∘   ! tz/ty cluster becomes s
						z -> s || Stop _                                         ;

define Undoubling ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || _ [Stop | f | s | wh] ∘   
                  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || [Stop | f | s | wh | h] _ ∘   
		          mm -> m, nn -> n, ngng -> ng, ngngw -> ngw        || [Stop | f | s | wh | h] _ ∘
				  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || _ ll ∘ 
                  ll -> l, rr -> r, gg -> g, ghh -> gh, ghhw -> ghw || [ll| rr | gg | ghh |ghhw] _ ∘ 
                  mm -> m, nn -> n, ngng -> ng, ngngw -> ngw        || [ll| rr | gg | ghh |ghhw] _ ;

def BaseFinalEndings ghw      -> qw , 
                     gh ("*") -> q  , 
                     g        -> k  ,
                     w        -> kw ,
                     t e      -> t a  ||   _ WordBoundary , 
                     t e      -> n    || V _ WordBoundary ∘
					 e        -> a    ||   _ WordBoundary ∘
				     gh q     -> q    ||   _ WordBoundary ;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!                        !!!
!!!      Rule Cascade      !!!
!!!                        !!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

define Rules [
    Lexicon                        ∘
	Lowercase                      ∘
	InsertMorphBoundary            ∘
	!! Iteration 1 !!
	Optative                       ∘
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 2 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 3 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 4 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 5 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 6 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	!! Iteration 7 !!
    JunctureConsonantInsertion     ∘
	teDropping                     ∘
	NoHopNoDropE                   ∘
    SemiAndFinalE                  ∘
	SemiFinalE                     ∘
	FinalConsonantDropAssimilation ∘
	WeakFinalC                     ∘
	UnmarkStrongGh                 ∘
	InterConsonantalE              ∘
    UvularDropping                 ∘
	VelarUvularRounding            ∘
    FinalE                         ∘
	TripleVowelCluster             ∘
    VowelDominance                 ∘
	CleanupMorphBoundary           ∘
	ClusterDevoicing               ∘
	BaseFinalEndings               ∘
	Undoubling
] ;


define Grammar [ Exceptions .P. Rules ] ;
push Grammar

